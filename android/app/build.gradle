apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlinx-serialization"
apply plugin: "kotlin-kapt"

apply from: "dependencies.gradle"
apply from: "sources.gradle"

android {
    compileSdkVersion 29

    signingConfigs {
        debug {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storePassword "android"
            storeFile file("debug.keystore")
        }
        release {
            final File signFile = rootProject.file('app/sign.properties')
            if (signFile.exists()) {
                Properties properties = new Properties()
                properties.load(new FileInputStream(signFile))
                keyAlias properties.releaseKeyAlias
                keyPassword properties.releaseKeyPassword
                storePassword properties.releaseStorePassword
                storeFile file(properties.releaseStoreFile)
            }
        }
    }

    androidExtensions {
        experimental = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.vivanov.tinkoffnews"
        minSdkVersion 21
        targetSdkVersion 29
        vectorDrawables.useSupportLibrary = true
        versionCode 1
        versionName "1.0.1"
        setProperty("archivesBaseName", "TinkoffNews-" + versionName + "-" + versionCode)
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude "META-INF/*.kotlin_module"
    }

    tasks.lint.enabled = false
}
