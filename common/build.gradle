apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"

apply plugin: "kotlin-android-extensions"

kotlin {

    targets {
        jvm("android")

        final def iosTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos")  \
                               ? presets.iosArm64 : presets.iosX64

        fromPreset(iosTarget, "ios") {
            binaries {
                framework {
                    baseName = "common"
                }
            }
        }
    }

    sourceSets {
        final def coroutinesVersion = "1.3.5-2-native-mt-1.4-M1"
        final def ktorVersion = "1.3.2-1.4-M1-2"
        final def serializationVersion = "0.20.0-1.4-M1"

        commonMain {
            kotlin.srcDir("src/commonMain/kotlin")

            dependencies {

                // Kotlin
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutinesVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializationVersion"

                // Ktor
                implementation "io.ktor:ktor-client-core:$ktorVersion"
                implementation "io.ktor:ktor-client-json:$ktorVersion"
                implementation "io.ktor:ktor-client-logging:$ktorVersion"
                implementation "io.ktor:ktor-client-serialization:$ktorVersion"
            }
        }

        androidMain {
            kotlin.srcDir("src/androidMain/kotlin")

            dependencies {

                // Kotlin
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializationVersion"

                // Ktor
                implementation "io.ktor:ktor-client-okhttp:$ktorVersion"
                implementation "io.ktor:ktor-client-json-jvm:$ktorVersion"
                implementation "io.ktor:ktor-client-logging-jvm:$ktorVersion"
                implementation "io.ktor:ktor-client-serialization-jvm:$ktorVersion"
            }
        }

        iosMain {
            kotlin.srcDir("src/iosMain/kotlin")

            dependencies {

                // Kotlin
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutinesVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serializationVersion"

                // Ktor
                implementation "io.ktor:ktor-client-ios:$ktorVersion"
                implementation "io.ktor:ktor-client-json-native:$ktorVersion"
                implementation "io.ktor:ktor-client-logging-native:$ktorVersion"
                implementation "io.ktor:ktor-client-serialization-native:$ktorVersion"
            }
        }
    }
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode
    dependsOn kotlin.targets.ios.binaries.getFramework(mode).linkTask

    from { kotlin.targets.ios.binaries.getFramework(mode).outputDirectory }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode
